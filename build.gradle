//file:noinspection GroovyAssignabilityCheck
plugins {
	alias libs.plugins.quilt.loom
	alias libs.plugins.quilt.licenser
	alias libs.plugins.minotaur
	//alias libs.plugins.machete
}

archivesBaseName = 'quilt_loading_screen'
version = "6.0.0+${libs.versions.minecraft.get()}"

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name "Modrinth"
				url 'https://api.modrinth.com/maven'
			}
		}
		filter {
			includeGroup 'maven.modrinth'
		}
	}
}

dependencies {
	minecraft libs.minecraft
	mappings(variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' })
	modApi libs.bundles.quilt

	modImplementation libs.midnightlib
	include libs.midnightlib

	modLocalRuntime libs.mod.menu
}

// TEMPORARY: until https://github.com/QuiltMC/quilt-loom/issues/28 gets fixed
processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 17
	it.sourceCompatibility = 17
}

jar.from 'LICENSE', 'README.md'
java.withSourcesJar()

license {
	rule 'HEADER'
	include '**/*.java'
}

modrinth {
	projectId = 'quilt-loading-screen'
	versionName = System.getenv().VERSION_NAME ?: versionNumber
	changelog = System.getenv().CHANGELOG ?: DEFAULT_CHANGELOG
	versionType = System.getenv().VERSION_TYPE ?: DEFAULT_VERSION_TYPE
	uploadFile = remapJar
	additionalFiles = [sourcesJar]
}
